plugins {
    id 'java'
    // https://plugins.gradle.org/plugin/xyz.jpenilla.run-paper
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
}

group = 'me.melani'
version = '1.0'

def mcVersion = '1.21.1'
def targetJavaVersion = 23

dependencies {
    // https://repo.papermc.io/#browse/browse:maven-public:io%2Fpapermc%2Fpaper%2Fpaper-api
    compileOnly "io.papermc.paper:paper-api:${mcVersion}-R0.1-SNAPSHOT"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = targetJavaVersion
}

tasks.runServer {
    minecraftVersion(mcVersion)
}

processResources {
    filesMatching('paper-plugin.yml') {
        expand 'version': version
    }
}